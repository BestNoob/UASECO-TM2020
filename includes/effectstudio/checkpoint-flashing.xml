<manialink id="EffectStudioCheckpointFlashing" name="EffectStudioCheckpointFlashing" version="1">
<quad posn="-160 90 20" sizen="320 180" bgcolor="F00F" hidden="true" opacity="0.5" id="QuadFlash"/>
<script><!--
 /*
 * ----------------------------------
 * Author:	undef.de
 * Website:	http://www.undef.name
 * License:	GPLv3
 * Version:	2015-05-25
 * ----------------------------------
 */
Void Flashlight () {
	declare CMlQuad QuadFlash <=> (Page.GetFirstChild("QuadFlash") as CMlQuad);

	QuadFlash.Visible = True;
	while (QuadFlash.Opacity > 0.1) {
		QuadFlash.Opacity -= 0.1;
		yield;
	}
	QuadFlash.Visible = False;
	QuadFlash.Opacity = 0.5;
}
main() {
	declare Integer RefreshInterval	= 100;
	declare Integer RefreshTime	= CurrentTime;
	declare Integer LastCheckpoint	= 0;

	while (True) {
		yield;
		if (!PageIsVisible || InputPlayer == Null) {
			continue;
		}
		if (CurrentTime > RefreshTime) {
			if (LastCheckpoint < InputPlayer.CurRace.Checkpoints.count && InputPlayer.RaceState == CTmMlPlayer::ERaceState::Running) {
				Flashlight();
				LastCheckpoint = InputPlayer.CurRace.Checkpoints.count;
			}

			// Reset RefreshTime
			RefreshTime = (CurrentTime + RefreshInterval);
		}
		if (InputPlayer.RaceState == CTmMlPlayer::ERaceState::BeforeStart) {
			// Reset
			LastCheckpoint = 0;
		}
	}
}
--></script>
</manialink>
